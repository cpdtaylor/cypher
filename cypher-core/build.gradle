

apply plugin: 'java-library'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'com.morpheusdata'
ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

repositories {
    mavenLocal()
	mavenCentral()
}

configurations {
    provided
}
 
java {
    withSourcesJar()
    withJavadocJar()
} 

dependencies {
    compileOnly("commons-logging:commons-logging:1.1.1")
    implementation('com.bertramlabs.plugins:hcl4j:0.3.0')
    implementation('jakarta.xml.bind:jakarta.xml.bind-api:3.0.0')
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'
    implementation 'org.apache.httpcomponents:httpasyncclient:4.1.4'
    implementation 'org.apache.httpcomponents:httpcore-nio:4.4.12'
    implementation "org.slf4j:slf4j-parent:1.7.26"
    implementation "org.slf4j:slf4j-api:1.7.26"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.5.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.10.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.10.5'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.12.2'
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
    testImplementation "org.codehaus.groovy:groovy-all:2.4.1"
    testImplementation "org.spockframework:spock-core:0.7-groovy-2.0"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'cypher-core'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'cypher-core'
                    description 'Cypher provides secure secret management with triple level encryption and pluggable backends.'
                    url 'https://github.com/gomorpheus/cypher'
                    scm {
                        url 'https://github.com/gomorpheus/cypher'
                        connection 'scm:https://git@github.com/gomorpheus/cypher.git'
                        developerConnection 'scm:git://github.com/gomorpheus/cypher.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}


task morpheusJavaDoc(type: Javadoc) {
  source = sourceSets.main.allJava
  title = "Cypher Core Docs"
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}
